apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'com.novoda.bintray-release'

def configuration = rootProject.ext.configuration
def libraries = rootProject.ext.libraries

android {
    compileSdkVersion configuration.compileSdkVersion
    buildToolsVersion configuration.buildToolsVersion

    defaultConfig {
        minSdkVersion configuration.minSdkVersion
        targetSdkVersion configuration.targetSdkVersion
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {

    // Forced using a specific version of Kotlin
    configurations.all {
        resolutionStrategy {
            force "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
        }
    }
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "com.android.support:design:${libraries.supportLibraryVersion}"
    compileOnly "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
}
repositories {
    mavenCentral()
}

// Disable creating javadocs
tasks.withType(Javadoc) {
    enabled = false
}

// Type these code in a single line to upload the library to jfrog.
// jcenter configuration for novoda's bintray-release
// $ ./gradlew clean build bintrayUpload
// -PbintrayUser=BINTRAY_USERNAME
// -PbintrayKey=BINTRAY_KEY
// -PdryRun=false
publish {
    userOrg = 'tonnyl'
    groupId = 'io.github.tonnyl'
    artifactId = 'light'
    publishVersion = '1.1.0'
    desc = 'The usual Snackbar, but elegant.'
    website = 'https://tonnyl.github.io'
}
